apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'swipe view in compose'
                    url = 'https://github.com/pseudoankit/ComposeSwipe'
//                    licenses {
//                        license {
//                            name = 'Stream License'
//                            url = 'https://github.com/GetStream/stream-chat-android/blob/main/LICENSE'
//                        }
//                    }
                    developers {
                        developer {
                            id = 'pseudoankit'
                            name = 'Ankit Kumar'
                            email = 'lostankit7@gmail.com'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/pseudoankit/ComposeSwipe.git'
                        developerConnection = 'scm:git:ssh://github.com/pseudoankit/ComposeSwipe.git'
                        url = 'https://https://github.com/pseudoankit/ComposeSwipe/tree/master/swipeable-card'
                    }
                }
            }
        }
    }
}